buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.scoverage:gradle-scoverage:1.1.0"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.5"
}

ext {
    junitVersion = '4.12'
    scalaMinorVersion = '2.11'
    scalaVersion = "$scalaMinorVersion.7"
    hzVersion = '3.6'
    scoverageVersion = '1.1.0'
}

apply plugin: "scala"
apply plugin: "maven-publish"
apply plugin: 'scoverage'

sourceCompatibility = 1.6
targetCompatibility = 1.6

task wrapper(type: Wrapper) {
    gradleVersion = "2.8"
}

version = tsVersion("1-alpha")

def tsVersion(version) {
    java.text.SimpleDateFormat tsFmt = new java.text.SimpleDateFormat('yyMMddHHmm')
    tsFmt.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')))
    return "${version}.b${tsFmt.format(new Date())}"
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:$junitVersion"
    scoverage "org.scoverage:scalac-scoverage-plugin_$scalaMinorVersion:$scoverageVersion", "org.scoverage:scalac-scoverage-runtime_$scalaMinorVersion:$scoverageVersion"
    //testCompile fileTree(dir: 'lib', include: '*.jar')
    //testCompile "com.hazelcast:hazelcast:$hzVersion:tests"
    compile "org.scala-lang:scala-reflect:$scalaVersion"
    compile "com.hazelcast:hazelcast:$hzVersion"
    compile "com.hazelcast:hazelcast-client:$hzVersion"
    compile "javax.cache:cache-api:1.0.0"
    compile "javax.transaction:transaction-api:1.1"
}

jar {
    baseName = "hazelcast-scala_$scalaMinorVersion"
    manifest {
        attributes "Implementation-Title": "hazelcast-scala", "Implementation-Version": version
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allScala
}
task docsJar(type: Jar, dependsOn: scaladoc) {
    classifier "docs"
    from scaladoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.hazelcast'
            artifactId jar.baseName

            from components.java
            artifact sourceJar
            artifact docsJar
            pom.withXml { xml ->
                def includeArtifacts = ['cache-api', 'transaction-api']
                xml.asNode().dependencies.dependency.findAll{ ! includeArtifacts.contains(it.artifactId.text()) }.each { dep ->
                    dep.scope[0].value = "provided"
                }
            }
        }
    }
}

bintray {
    user = System.properties.bintrayUser ?: "n/a"
    key = System.properties.bintrayApiKey ?: "n/a"
    dryRun = (user == "n/a" || key == "n/a")
    publish = !dryRun
    publications = ['maven']
    pkg {
        repo = System.properties.bintrayRepo
        name = System.properties.bintrayName
//        version {
//            name = version
//        }
    }
}
